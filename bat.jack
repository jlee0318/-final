class Bat 
{
    field int x, y;
    field int width, height;
    field int direction; // 1 for up, 2 for down

    constructor Bat new(int Ax, int Ay, int Awidth, int Aheight) 
    {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let direction = 2;

        do draw();

        return this;
    }

    method void dispose() 
    {
        do Memory.deAlloc(this);
        return;
    }

    method void draw()
    {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + width - 1, y + height - 1);
        return;
    }

    method void erase() 
    {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + width - 1, y + height - 1);
        return;
    }

    method void setDirection(int Adirection) 
    {
	    let direction = Adirection;
        return;
    }

    method int getY()
    {
        return y;
    }

    method int getWidth()
    {
        return width;
    }

    method void setWidth(int Awidth) 
    {
        do erase();
	    let width = Awidth;
        do draw();
        return;
    }

    method void move() 
    {
	    if (direction = 1) 
        {
            let y = y - 1;
            if (y < 0) 
            {
                let y = 0;
            }
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y + height, x + width - 1, y + height);
            do Screen.setColor(true);
   	        do Screen.drawRectangle(x, y, x + width - 1, y);
	    } 
        else 
        {
            let y = y + 1;
            if ((y + height) > 256) 
            {
                let y = 256 - height;
            }
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y - 1, x + width - 1, y - 1);
            do Screen.setColor(true);
	        do Screen.drawRectangle(x, y + height - 1, x + width - 1, y + height - 1);
        }

        return;
    }
}