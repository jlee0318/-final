class Bat 
{
    field int x, y;
    field int width, height;
    field int direction; // 1 for up, 2 for down

    constructor Bat new(int Ax, int Ay, int Awidth, int Aheight) 
    {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let direction = 2;

        do draw();

        return this;
    }

    method void dispose() 
    {
        do Memory.deAlloc(this);
        return;
    }

    method void draw()
    {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    method void erase() 
    {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    method void setDirection(int Adirection) 
    {
	    let direction = Adirection;
        return;
    }

    method int getY()
    {
        return y;
    }

    method int getWidth()
    {
        return width;
    }

    method void setWidth(int Awidth) 
    {
        do erase();
	    let width = Awidth;
        do draw();
        return;
    }

    method void move() 
    {
	    if (direction = 1) 
        {
            let y = y - 2;
            if (y < 0) 
            {
                let y = 0;
            }
            do Screen.setColor(false);
            do Screen.drawRectangle(x, (y + height) + 1, x + width, (y + height) + 2);
            do Screen.setColor(true);
   	        do Screen.drawRectangle(x, y, x + width, y + 1);
	    } 
        else 
        {
            let y = y + 2;
            if ((y + height) > 255) 
            {
                let y = 255 - height;
            }
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y - 2, x + width, y - 1);
            do Screen.setColor(true);
	        do Screen.drawRectangle(x, y + height - 1, x + width, y + height);
        }

        return;
    }
}